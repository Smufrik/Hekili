## https://github.com/simulationcraft/simc/blob/thewarwithin/ActionPriorityLists/deathknight_blood.simc
## Up to date with SimC: April 14 2025 - 7cb5ea4

actions.precombat+=/variable,name=bone_shield_refresh_value,value=7
actions.precombat+=/variable,name=death_strike_dump_amount,value=settings.death_strike_pool_amount
actions.precombat+=/deaths_caress

actions+=/mind_freeze
# Trinkets
actions+=/variable,name=trinket_1_buffs,value=trinket.1.has_use_buff|(trinket.1.has_buff.strength|trinket.1.has_buff.mastery|trinket.1.has_buff.versatility|trinket.1.has_buff.haste|trinket.1.has_buff.crit)|trinket.1.is.mirror_of_fractured_tomorrows
actions+=/variable,name=trinket_2_buffs,value=trinket.2.has_use_buff|(trinket.2.has_buff.strength|trinket.2.has_buff.mastery|trinket.2.has_buff.versatility|trinket.2.has_buff.haste|trinket.2.has_buff.crit)|trinket.2.is.mirror_of_fractured_tomorrows
actions+=/variable,name=damage_trinket_priority,op=setif,value=2,value_else=1,condition=!variable.trinket_2_buffs&trinket.2.ilvl>=trinket.1.ilvl|variable.trinket_1_buffs
actions+=/variable,name=rp_deficit_threshold,value=15+(10*talent.relish_in_blood.enabled)+(3*talent.runic_attenuation.enabled)+(spell_targets.heart_strike*talent.heartbreaker.enabled*2)
actions+=/use_item,name=fyralath_the_dreamrender,if=dot.mark_of_fyralath.ticking
# Prioritize damage dealing on use trinkets over trinkets that give buffs
actions+=/use_item,slot=trinket1,if=!variable.trinket_1_buffs&(variable.damage_trinket_priority=1|trinket.2.cooldown.remains|!trinket.2.has_cooldown)
actions+=/use_item,slot=trinket2,if=!variable.trinket_2_buffs&(variable.damage_trinket_priority=2|trinket.1.cooldown.remains|!trinket.1.has_cooldown)
actions+=/use_item,slot=main_hand,if=!equipped.fyralath_the_dreamrender&(variable.trinket_1_buffs|trinket.1.cooldown.remains)&(variable.trinket_2_buffs|trinket.2.cooldown.remains)
actions+=/use_item,slot=trinket1,if=variable.trinket_1_buffs&(buff.dancing_rune_weapon.up|!talent.dancing_rune_weapon|cooldown.dancing_rune_weapon.remains>20)&(trinket.2.cooldown.remains|!trinket.2.has_cooldown|variable.trinket_2_buffs)
actions+=/use_item,slot=trinket2,if=variable.trinket_2_buffs&(buff.dancing_rune_weapon.up|!talent.dancing_rune_weapon|cooldown.dancing_rune_weapon.remains>20)&(trinket.1.cooldown.remains|!trinket.1.has_cooldown|variable.trinket_1_buffs)
# Use Icebound Fortitude if you're taking sufficient damage and you don't have Dancing Rune Weapon or Vampiric Blood up.
actions+=/icebound_fortitude,if=tanking&incoming_damage_5s>=ibf_damage&!(buff.dancing_rune_weapon.up|buff.vampiric_blood.up)
actions+=/vampiric_blood,if=tanking&incoming_damage_5s>=vb_damage&!(buff.dancing_rune_weapon.up|buff.icebound_fortitude.up)
# Use Rune Tap if you're taking sufficient damage and you don't have Dancing Rune Weapon, Vampiric Blood, or Icebound Fortitude up.
actions+=/rune_tap,if=tanking&incoming_damage_5s>=rt_damage&!(buff.dancing_rune_weapon.up|buff.vampiric_blood.up|buff.icebound_fortitude.up)
actions+=/call_action_list,name=high_prio_actions
actions+=/run_action_list,name=san_drw,if=hero_tree.sanlayn&buff.dancing_rune_weapon.up
actions+=/call_action_list,name=san_cds,if=hero_tree.sanlayn
actions+=/run_action_list,name=sanlayn,strict=1,if=!hero_tree.deathbringer
actions+=/call_action_list,name=db_cds,if=hero_tree.deathbringer
actions+=/run_action_list,name=deathbringer,strict=1,if=hero_tree.deathbringer

actions.db_cds+=/reapers_mark
actions.db_cds+=/dancing_rune_weapon
actions.db_cds+=/bonestorm,if=buff.bone_shield.stack>=5&(!talent.shattering_bone.enabled|death_and_decay.ticking)
actions.db_cds+=/tombstone,if=buff.bone_shield.stack>=8&(!talent.shattering_bone.enabled|death_and_decay.ticking)&cooldown.dancing_rune_weapon.remains>=25
actions.db_cds+=/abomination_limb,if=!buff.dancing_rune_weapon.up

actions.deathbringer+=/variable,name=heart_strike_rp_drw,value=(25+spell_targets.heart_strike*talent.heartbreaker.enabled*2)
actions.deathbringer+=/marrowrend,if=buff.exterminate.up
actions.deathbringer+=/marrowrend,if=buff.bone_shield.stack<6&!dot.bonestorm.ticking
actions.deathbringer+=/blood_boil,if=pet.dancing_rune_weapon.active&!drw.bp_ticking
actions.deathbringer+=/soul_reaper,if=buff.reaper_of_souls.up&cooldown.dancing_rune_weapon.remains
actions.deathbringer+=/blooddrinker,if=!buff.dancing_rune_weapon.up&active_enemies<=2&buff.coagulopathy.remains>3
actions.deathbringer+=/death_strike
actions.deathbringer+=/consumption
actions.deathbringer+=/blood_boil,if=charges_fractional>=1.5
actions.deathbringer+=/heart_strike,if=rune>=1|rune.time_to_2<gcd
actions.deathbringer+=/blood_boil
actions.deathbringer+=/heart_strike
actions.deathbringer+=/arcane_torrent,if=runic_power.deficit>20
actions.deathbringer+=/deaths_caress,if=buff.bone_shield.stack<11

actions.high_prio_actions+=/death_strike,if=settings.save_blood_shield&buff.blood_shield.up&buff.blood_shield.remains<=gcd.max
actions.high_prio_actions+=/blood_tap,use_off_gcd=1,if=(rune<=2&rune.time_to_3>gcd.max&charges_fractional>=1.8)
actions.high_prio_actions+=/blood_tap,use_off_gcd=1,if=(rune<=1&rune.time_to_3>gcd.max)
actions.high_prio_actions+=/use_items
actions.high_prio_actions+=/raise_dead,use_off_gcd=1
actions.high_prio_actions+=/deaths_caress,if=buff.bone_shield.remains<gcd.max*2
actions.high_prio_actions+=/death_strike,if=buff.coagulopathy.up&buff.coagulopathy.remains<=gcd.max*2
actions.high_prio_actions+=/any_dnd,if=!buff.death_and_decay.up
actions.high_prio_actions+=/blood_boil,if=dot.blood_plague.remains<gcd.max*2
actions.high_prio_actions+=/soul_reaper,if=active_enemies=1&(target.time_to_pct_35<5)&target.time_to_die>(dot.soul_reaper.remains+5)&(!hero_tree.sanlayn|pet.dancing_rune_weapon.remains<5)
actions.high_prio_actions+=/rune_tap,use_off_gcd=1,if=rune>3

actions.san_cds+=/abomination_limb,if=!buff.dancing_rune_weapon.up
actions.san_cds+=/dancing_rune_weapon
actions.san_cds+=/bonestorm,if=buff.death_and_decay.up&buff.bone_shield.stack>5&cooldown.dancing_rune_weapon.remains>15
actions.san_cds+=/tombstone,if=(!buff.dancing_rune_weapon.up&buff.death_and_decay.up)&buff.bone_shield.stack>5&runic_power.deficit>=30&cooldown.dancing_rune_weapon.remains>25

actions.san_drw+=/bonestorm,if=buff.death_and_decay.up&buff.bone_shield.stack>5
actions.san_drw+=/death_strike,if=(active_enemies=1|buff.luck_of_the_draw.up)&runic_power.deficit<variable.rp_deficit_threshold
actions.san_drw+=/blood_boil,if=!drw.bp_ticking
actions.san_drw+=/heart_strike
actions.san_drw+=/death_strike
actions.san_drw+=/consumption
actions.san_drw+=/blood_boil


actions.sanlayn+=/variable,name=death_strike_pre_essence_dump_amount,value=20
actions.sanlayn+=/variable,name=heart_strike_rp_drw,value=(21+spell_targets.heart_strike*talent.heartbreaker.enabled*2)
actions.sanlayn+=/death_strike,if=buff.coagulopathy.remains<=gcd
actions.sanlayn+=/heart_strike,if=buff.vampiric_strike.up
actions.sanlayn+=/blooddrinker,if=!buff.dancing_rune_weapon.up&active_enemies<=2&buff.coagulopathy.remains>3
actions.sanlayn+=/death_strike,if=runic_power.deficit<variable.rp_deficit_threshold
actions.sanlayn+=/marrowrend,if=!dot.bonestorm.ticking&buff.bone_shield.stack<variable.bone_shield_refresh_value&runic_power.deficit>20
actions.sanlayn+=/death_strike
actions.sanlayn+=/heart_strike,if=rune>1
actions.sanlayn+=/consumption
actions.sanlayn+=/blood_boil
actions.sanlayn+=/heart_strike